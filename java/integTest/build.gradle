plugins {
    id 'java-library'
    id "com.google.osdetector" version "1.7.3"
}

repositories {
    mavenCentral()
    mavenLocal()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    def classifier 
    if (osdetector.os == 'linux' && osdetector.release.id == 'alpine') {
        classifier = "linux_musl-${osdetector.arch}"
    }
    else {
        classifier = osdetector.classifier
    }
    // Use published GLIDE artifact
    implementation group: 'io.valkey', name: 'valkey-glide', version: project.ext.defaultReleaseVersion, classifier: classifier
    
    // Use published jedis-compatibility artifact
    testImplementation group: 'io.valkey', name: 'valkey-glide-jedis-compatibility', version: project.ext.defaultReleaseVersion, classifier: osdetector.classifier

    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.18.0'
    implementation 'com.google.code.gson:gson:2.13.2'

    // Add commons-pool2 as compile-time dependency for tests
    implementation group: 'org.apache.commons', name: 'commons-pool2', version: '2.12.1'



    // junit
    testImplementation 'org.mockito:mockito-junit-jupiter:3.12.4'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.10.2'

    // semver4j for semantic versioning
    implementation group: 'org.semver4j', name: 'semver4j', version: '5.6.0'

    //lombok
    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'
}

def standaloneHosts = ''
def standaloneTlsHosts = ''
def clusterHosts = ''
def clusterTlsHosts = ''
def azClusterHosts = ''

// Platform-specific Python executable and WSL handling
def isWindows = System.getProperty('os.name').toLowerCase().contains('windows')
def pythonCmd = isWindows ? ['wsl', '--', 'python3'] : ['python3']

// Use integration test IP from environment variable if available, otherwise default to localhost
def clusterHost = System.getenv('VALKEY_INTEG_TEST_IP') ?: '127.0.0.1'

// Get workspace root - use system property or derive from project root
def workspaceRoot = System.getProperty('workspace.root') ?: "${project.rootDir}/../.."

ext {
    extractAddressesFromClusterManagerOutput = { String output ->
        for (def line : output.split("\n")) {
            if (!line.startsWith("CLUSTER_NODES="))
                continue

            return line.split("=")[1]
        }
        return ''
    }
}

tasks.register('stopAllAfterTests') {
    doLast {
        exec {
            workingDir "${project.rootDir}/../utils"
            commandLine *pythonCmd, 'cluster_manager.py', 'stop', '--prefix', 'cluster', '--keep-folder', '--host', clusterHost
            ignoreExitValue true // ignore fail if servers are stopped before
        }
        exec {
            workingDir "${project.rootDir}/../utils"
            commandLine *pythonCmd, 'cluster_manager.py', '--tls', 'stop', '--prefix', 'tls-cluster', '--keep-folder', '--host', clusterHost
            ignoreExitValue true
        }
    }
}

// We need to call for stop before and after the test, but gradle doesn't support executing a task
// twice. So there are two identical tasks with different names.
// We need to call for stop in case if previous test run was interrupted/crashed and didn't stop.
tasks.register('stopAllBeforeTests') {
    doLast {
        println "=== DEBUG: stopAllBeforeTests task running ==="
        try {
            exec {
                workingDir "${project.rootDir}/../utils"
                commandLine *pythonCmd, 'cluster_manager.py', 'stop', '--prefix', 'cluster', '--host', clusterHost
                ignoreExitValue true // ignore fail if servers are stopped before
            }
        } catch (Exception e) {
            println "=== INFO: No cluster servers to stop (expected): ${e.message} ==="
        }
        
        try {
            exec {
                workingDir "${project.rootDir}/../utils"
                commandLine *pythonCmd, 'cluster_manager.py', '--tls', 'stop', '--prefix', 'tls-cluster', '--host', clusterHost
                ignoreExitValue true
            }
        } catch (Exception e) {
            println "=== INFO: No TLS cluster servers to stop (expected): ${e.message} ==="
        }
        println "=== DEBUG: stopAllBeforeTests task completed ==="
    }
}

// delete dirs if stop failed due to https://github.com/valkey-io/valkey-glide/issues/849
tasks.register('clearDirs', Delete) {
    delete "${project.rootDir}/../utils/clusters"
}

tasks.register('startCluster') {
    doLast {
        if (System.getProperty("cluster-endpoints") == null) {
            if (isWindows) {
                println "=== Starting cluster setup via WSL bash script ==="
            }
            
            new ByteArrayOutputStream().withStream { os ->
                if (isWindows) {
                    // Use WSL to run bash script
                    exec {
                        workingDir "${project.rootDir}/../utils"
                        commandLine 'wsl', '--', 'bash', './start-cluster.sh'
                        standardOutput = os
                        errorOutput = System.err  // Show debug output from script
                    }
                    def output = os.toString()
                    def matcher = output =~ /CLUSTER_HOSTS=([^\n\r]+)/
                    if (matcher.find()) {
                        clusterHosts = matcher.group(1)
                    } else {
                        throw new Exception("Could not extract cluster hosts from script output")
                    }
                } else {
                    exec {
                        workingDir "${project.rootDir}/../utils"
                        def args = [*pythonCmd, 'cluster_manager.py', 'start', '--cluster-mode']
                        if (System.getProperty("tls") == 'true') args.add(2, '--tls')
                        commandLine args
                        standardOutput = os
                        errorOutput = System.out
                    }
                    clusterHosts = extractAddressesFromClusterManagerOutput(os.toString())
                }
                println "=== Cluster hosts: ${clusterHosts} ==="
            }
        } else {
            clusterHosts = System.getProperty("cluster-endpoints")
        }
    }
}

tasks.register('startClusterForAz') {
    doLast {
        if (System.getProperty("cluster-endpoints") == null) {
            new ByteArrayOutputStream().withStream { os ->
                if (isWindows) {
                    // Use WSL to run AZ bash script
                    exec {
                        workingDir "${project.rootDir}/../utils"
                        commandLine 'wsl', '--', 'bash', './start-cluster-az.sh'
                        standardOutput = os
                        errorOutput = System.err  // Show debug output from script
                    }
                    def output = os.toString()
                    def matcher = output =~ /AZ_CLUSTER_HOSTS=([^\n\r]+)/
                    if (matcher.find()) {
                        azClusterHosts = matcher.group(1)
                    } else {
                        throw new Exception("Could not extract AZ cluster hosts from script output")
                    }
                } else {
                    exec {
                        workingDir "${project.rootDir}/../utils"
                        def args = [*pythonCmd, 'cluster_manager.py', 'start', '--cluster-mode', '-r', '4']
                        if (System.getProperty("tls") == 'true') args.add(2, '--tls')
                        commandLine args
                        standardOutput = os
                    }
                    azClusterHosts = extractAddressesFromClusterManagerOutput(os.toString())
                }
            }
        } else {
            azClusterHosts = System.getProperty("cluster-endpoints")
        }
    }
}

tasks.register('beforeTests') {
    doLast {
        println "=== DEBUG: beforeTests task running ==="
    }
}
tasks.register('afterTests') {
    doLast {
        println "=== DEBUG: afterTests task running ==="
    }
}
tasks.register('startStandalone') {
    doLast {
        println "=== DEBUG: startStandalone task starting ==="
        if (System.getProperty("standalone-endpoints") == null) {
            try {
                new ByteArrayOutputStream().withStream { os ->
                    exec {
                        workingDir "${project.rootDir}/../utils"
                        if (isWindows) {
                            def args = ['wsl', '--', 'python3', 'cluster_manager.py', 'start', '-r', '0']
                            if (System.getProperty("tls") == 'true') args.add(5, '--tls')
                            commandLine args
                        } else {
                            def args = ['python3', 'cluster_manager.py', 'start', '-r', '0']
                            if (System.getProperty("tls") == 'true') args.add(2, '--tls')
                            commandLine args
                        }
                        standardOutput = os
                        errorOutput = System.err  // Show Python logging
                    }
                    def output = os.toString()
                    println "=== Standalone command output: ${output.take(200)}... ==="
                    standaloneHosts = extractAddressesFromClusterManagerOutput(output)
                    println "=== Standalone hosts: ${standaloneHosts} ==="
                }
            } catch (Exception e) {
                println "=== ERROR: startStandalone failed: ${e.message} ==="
                standaloneHosts = "127.0.0.1:6379"  // Fallback value
            }
        } else {
            standaloneHosts = System.getProperty("standalone-endpoints")
        }
        println "=== DEBUG: startStandalone task completed ==="
    }
}

tasks.register('startStandaloneTls') {
    doLast {
        println "=== DEBUG: startStandaloneTls task starting ==="
        new ByteArrayOutputStream().withStream { os ->
            exec {
                workingDir "${project.rootDir}/../utils"
                if (isWindows) {
                    commandLine 'wsl', '--', 'python3', 'cluster_manager.py', '--tls', 'start', '-r', '0'
                } else {
                    commandLine 'python3', 'cluster_manager.py', '--tls', 'start', '-r', '0'
                }
                standardOutput = os
                errorOutput = System.err  // Show Python logging
            }
            def output = os.toString()
            println "=== Standalone TLS command output: ${output.take(200)}... ==="
            standaloneTlsHosts = extractAddressesFromClusterManagerOutput(output)
            println "=== Standalone TLS hosts: ${standaloneTlsHosts} ==="
        }
        println "=== DEBUG: startStandaloneTls task completed ==="
    }
}

tasks.register('startClusterTls') {
    doLast {
        new ByteArrayOutputStream().withStream { os ->
            if (isWindows) {
                // Use WSL to run TLS bash script
                exec {
                    workingDir "${project.rootDir}/../utils"
                    commandLine 'wsl', '--', 'bash', './start-cluster-tls.sh'
                    standardOutput = os
                    errorOutput = System.err  // Show debug output from script
                }
                def output = os.toString()
                def matcher = output =~ /CLUSTER_TLS_HOSTS=([^\n\r]+)/
                if (matcher.find()) {
                    clusterTlsHosts = matcher.group(1)
                } else {
                    throw new Exception("Could not extract TLS cluster hosts from script output")
                }
                
                // Copy certificates from script output location if needed
                exec {
                    commandLine 'wsl', '--', 'bash', '-c',
                        'find ${workspaceRoot}/utils/clusters -name "tls" -type d -exec cp -r {} ${workspaceRoot}/utils/tls_crts \\; 2>/dev/null || true'
                    ignoreExitValue true
                }
            } else {
                exec {
                    workingDir "${project.rootDir}/../utils"
                    commandLine(*pythonCmd, 'cluster_manager.py', 'start', '--tls', '--cluster-mode')
                    standardOutput = os
                }
                clusterTlsHosts = extractAddressesFromClusterManagerOutput(os.toString())
            }
        }
    }
}

beforeTests.dependsOn stopAllBeforeTests
stopAllBeforeTests.finalizedBy clearDirs
clearDirs.finalizedBy startStandalone
clearDirs.finalizedBy startStandaloneTls
clearDirs.finalizedBy startCluster
clearDirs.finalizedBy startClusterTls
clearDirs.finalizedBy startClusterForAz
afterTests.finalizedBy stopAllAfterTests
compileTestJava.dependsOn(':client:publishToMavenLocal', ':jedis-compatibility:publishToMavenLocal')

tasks.withType(Test) {
    dependsOn(':client:publishToMavenLocal', ':jedis-compatibility:publishToMavenLocal')
    useJUnitPlatform()
    if (!project.gradle.startParameter.taskNames.contains(':integTest:modulesTest')) {
        dependsOn 'beforeTests'
        finalizedBy 'afterTests'
    }

    doFirst {
        systemProperty 'test.server.standalone', standaloneHosts
        systemProperty 'test.server.standalone.tls', standaloneTlsHosts
        systemProperty 'test.server.cluster', clusterHosts
        systemProperty 'test.server.cluster.tls', clusterTlsHosts
        systemProperty 'test.server.azcluster', azClusterHosts
        systemProperty 'test.server.tls', System.getProperty("tls")
    }

    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }

    minHeapSize = "2048m" // Initial heap size. Needed for max size tests.
    maxHeapSize = "2048m" // Maximum heap size. Needed for max size tests.
    
    // Native library path for GLIDE FFI - needed for Jedis compatibility tests
    jvmArgs "-Djava.library.path=${project.rootDir}/../target/release"
    
    // Disable modularity for jedis compatibility tests
    if (name.contains('jedis') || filter.includePatterns.any { it.contains('jedis') }) {
        jvmArgs += "--add-opens=java.base/java.lang=ALL-UNNAMED"
        jvmArgs += "--add-opens=java.base/java.util=ALL-UNNAMED"
    }

    afterTest { desc, result ->
        logger.quiet "${desc.className}.${desc.name}: ${result.resultType} ${(result.getEndTime() - result.getStartTime())/1000}s"
    }

    finalizedBy jacocoTestReport, jacocoTestCoverageVerification
}

// JaCoCo section (code coverage by integration tests)
jacocoTestReport {
    sourceDirectories.setFrom(files("${project.rootDir}/client/src/main/java"))
    classDirectories.setFrom(files("$buildDir/classes"))
    executionData.setFrom(
        files("$buildDir/jacoco/test.exec"),
        files("$buildDir/jacoco/pubsubTest.exec")
    )
}
jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            excludes = [
                'glide.models.protobuf.*',
                'glide.com.google.*', // shadowed protobuf
                'glide.io.netty.*' // shadowed netty
            ]
        }
    }
}
// End of JaCoCo section

test {
    filter {
        excludeTestsMatching 'glide.PubSubTests'
        excludeTestsMatching 'glide.modules.*'
    }
}

tasks.register('pubsubTest', Test) {
    filter {
        includeTestsMatching 'glide.PubSubTests'
    }
    finalizedBy jacocoTestReport, jacocoTestCoverageVerification
}

tasks.register('modulesTest', Test) {
    doFirst {
        clusterHosts = System.getProperty("cluster-endpoints")
    }

    filter {
        includeTestsMatching 'glide.modules.*'
    }
}
