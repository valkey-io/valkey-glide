plugins {
    id 'java-library'
    id "com.google.osdetector" version "1.7.3"
}

repositories {
    mavenCentral()
    mavenLocal()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    def classifier 
    if (osdetector.os == 'linux' && osdetector.release.id == 'alpine') {
        classifier = "linux_musl-${osdetector.arch}"
    }
    else {
        classifier = osdetector.classifier
    }
    // Use published GLIDE artifact
    implementation group: 'io.valkey', name: 'valkey-glide', version: project.ext.defaultReleaseVersion, classifier: classifier
    
    // Use published jedis-compatibility artifact
    testImplementation group: 'io.valkey', name: 'valkey-glide-jedis-compatibility', version: project.ext.defaultReleaseVersion, classifier: osdetector.classifier

    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.18.0'
    implementation 'com.google.code.gson:gson:2.13.2'

    // Add commons-pool2 as compile-time dependency for tests
    implementation group: 'org.apache.commons', name: 'commons-pool2', version: '2.12.1'



    // junit
    testImplementation 'org.mockito:mockito-junit-jupiter:3.12.4'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.10.2'

    // semver4j for semantic versioning
    implementation group: 'org.semver4j', name: 'semver4j', version: '5.6.0'

    //lombok
    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'
}

def standaloneHosts = ''
def standaloneTlsHosts = ''
def clusterHosts = ''
def clusterTlsHosts = ''
def azClusterHosts = ''

// Platform-specific Python executable and WSL handling
def isWindows = System.getProperty('os.name').toLowerCase().contains('windows')
def pythonCmd = isWindows ? ['wsl', '--', 'python3'] : ['python3']

// Use integration test IP from environment variable if available, otherwise default to localhost
def clusterHost = System.getenv('VALKEY_INTEG_TEST_IP') ?: '127.0.0.1'

// Get workspace root - use system property or derive from project root
def workspaceRoot = System.getProperty('workspace.root') ?: "${project.rootDir}/../.."

ext {
    extractAddressesFromClusterManagerOutput = { String output ->
        for (def line : output.split("\n")) {
            if (!line.startsWith("CLUSTER_NODES="))
                continue

            return line.split("=")[1]
        }
        return ''
    }
}

tasks.register('stopAllAfterTests') {
    doLast {
        exec {
            workingDir "${project.rootDir}/../utils"
            commandLine *pythonCmd, 'cluster_manager.py', 'stop', '--prefix', 'cluster', '--keep-folder', '--host', clusterHost
            ignoreExitValue true // ignore fail if servers are stopped before
        }
        exec {
            workingDir "${project.rootDir}/../utils"
            commandLine *pythonCmd, 'cluster_manager.py', '--tls', 'stop', '--prefix', 'tls-cluster', '--keep-folder', '--host', clusterHost
            ignoreExitValue true
        }
    }
}

// We need to call for stop before and after the test, but gradle doesn't support executing a task
// twice. So there are two identical tasks with different names.
// We need to call for stop in case if previous test run was interrupted/crashed and didn't stop.
tasks.register('stopAllBeforeTests') {
    doLast {
        exec {
            workingDir "${project.rootDir}/../utils"
            commandLine *pythonCmd, 'cluster_manager.py', 'stop', '--prefix', 'cluster', '--host', clusterHost
            ignoreExitValue true // ignore fail if servers are stopped before
        }
        exec {
            workingDir "${project.rootDir}/../utils"
            commandLine *pythonCmd, 'cluster_manager.py', '--tls', 'stop', '--prefix', 'tls-cluster', '--host', clusterHost
            ignoreExitValue true
        }
    }
}

// delete dirs if stop failed due to https://github.com/valkey-io/valkey-glide/issues/849
tasks.register('clearDirs', Delete) {
    delete "${project.rootDir}/../utils/clusters"
}

tasks.register('startCluster') {
    doLast {
        if (System.getProperty("cluster-endpoints") == null) {
            if (isWindows) {
                println "=== Starting cluster setup ==="
            }
            
            new ByteArrayOutputStream().withStream { os ->
                exec {
                    workingDir "${project.rootDir}/../utils"
                    def args = [*pythonCmd, 'cluster_manager.py', 'start', '--cluster-mode', '--host', clusterHost]
                    if (System.getProperty("tls") == 'true') args.add(2, '--tls')
                    commandLine args
                    standardOutput = os
                    errorOutput = System.out
                }
                clusterHosts = extractAddressesFromClusterManagerOutput(os.toString())
                println "=== Cluster hosts: ${clusterHosts} ==="
            }
        } else {
            clusterHosts = System.getProperty("cluster-endpoints")
        }
    }
}

tasks.register('startClusterForAz') {
    doLast {
        if (System.getProperty("cluster-endpoints") == null) {
            new ByteArrayOutputStream().withStream { os ->
                exec {
                    workingDir "${project.rootDir}/../utils"
                    def args = [*pythonCmd, 'cluster_manager.py', 'start', '--cluster-mode', '--host', clusterHost]
                    if (System.getProperty("tls") == 'true') args.add(2, '--tls')
                    commandLine args
                    standardOutput = os
                }
                azClusterHosts = extractAddressesFromClusterManagerOutput(os.toString())
            }
        } else {
            azClusterHosts = System.getProperty("cluster-endpoints")
        }
    }
}

tasks.register('beforeTests') {}
tasks.register('afterTests') {}
tasks.register('startStandalone') {
    doLast {
        if (System.getProperty("standalone-endpoints") == null) {
            // Debug and setup WSL environment on Windows
            if (isWindows) {
                println "=== WSL Debug Info ==="
                exec {
                    commandLine 'wsl', '--', 'bash', '-c', 'echo "WSL Status: OK" && pwd && whoami'
                    ignoreExitValue true
                }
                exec {
                    commandLine 'wsl', '--', 'bash', '-c', 'echo "Python: $(which python3)" && echo "Valkey: $(which valkey-server || which redis-server || echo "NOT FOUND")"'
                    ignoreExitValue true
                }
                
                // Ensure cluster directory exists and has proper permissions
                println "=== Setting up cluster directories ==="
                exec {
                    commandLine 'wsl', '--', 'bash', '-c', "cd \"\${workspaceRoot}/utils\" && mkdir -p clusters && chmod 755 clusters && ls -la clusters"
                    ignoreExitValue true
                }
                println "=== Starting cluster manager ==="
            }
            
            new ByteArrayOutputStream().withStream { os ->
                exec {
                    workingDir "${project.rootDir}/../utils"
                    def args = [*pythonCmd, 'cluster_manager.py', 'start', '--host', clusterHost]
                    if (System.getProperty("tls") == 'true') args.add(2, '--tls')
                    commandLine args
                    standardOutput = os
                    errorOutput = System.out // Show errors immediately
                }
                standaloneHosts = extractAddressesFromClusterManagerOutput(os.toString())
                println "=== Standalone hosts: ${standaloneHosts} ==="
            }
        } else {
            standaloneHosts = System.getProperty("standalone-endpoints")
        }
    }
}

tasks.register('startStandaloneTls') {
    doLast {
        new ByteArrayOutputStream().withStream { os ->
            exec {
                workingDir "${project.rootDir}/../utils"
                commandLine(*pythonCmd, 'cluster_manager.py', 'start', '--tls')
                standardOutput = os
            }
            standaloneTlsHosts = extractAddressesFromClusterManagerOutput(os.toString())
            
            // Copy certificates from WSL to Windows if needed
            if (isWindows) {
                exec {
                    commandLine 'wsl', '--', 'bash', '-c',
                        'echo "=== WSL tls_crts ===" && ls -la /mnt/d/a/valkey-glide/valkey-glide/utils/tls_crts/ && echo "=== Windows tls_crts ===" && ls -la /mnt/d/a/valkey-glide/valkey-glide/utils/tls_crts/'
                    ignoreExitValue true
                }
                // Ensure certificates have Unix line endings (remove any Windows \r)
                exec {
                    commandLine 'wsl', '--', 'bash', '-c',
                        'cd ${workspaceRoot}/utils/tls_crts && for f in *.crt *.key *.pem; do [ -f "$f" ] && sed -i "s/\\r//g" "$f"; done'
                    ignoreExitValue true
                }
            }
        }
    }
}

tasks.register('startClusterTls') {
    doLast {
        new ByteArrayOutputStream().withStream { os ->
            exec {
                workingDir "${project.rootDir}/../utils"
                commandLine(*pythonCmd, 'cluster_manager.py', 'start', '--tls', '--cluster-mode')
                standardOutput = os
            }
            clusterTlsHosts = extractAddressesFromClusterManagerOutput(os.toString())
            
            // Copy certificates from WSL to Windows if needed
            if (isWindows) {
                exec {
                    commandLine 'wsl', '--', 'bash', '-c',
                        'echo "=== WSL tls_crts ===" && ls -la /mnt/d/a/valkey-glide/valkey-glide/utils/tls_crts/ && echo "=== Windows tls_crts ===" && ls -la /mnt/d/a/valkey-glide/valkey-glide/utils/tls_crts/'
                    ignoreExitValue true
                }
                // Ensure certificates have Unix line endings (remove any Windows \r)
                exec {
                    commandLine 'wsl', '--', 'bash', '-c',
                        'cd ${workspaceRoot}/utils/tls_crts && for f in *.crt *.key *.pem; do [ -f "$f" ] && sed -i "s/\\r//g" "$f"; done'
                    ignoreExitValue true
                }
            }
        }
    }
}

beforeTests.dependsOn 'stopAllBeforeTests'
stopAllBeforeTests.finalizedBy 'clearDirs'
clearDirs.finalizedBy 'startStandalone'
clearDirs.finalizedBy 'startStandaloneTls'
clearDirs.finalizedBy 'startCluster'
clearDirs.finalizedBy 'startClusterTls'
clearDirs.finalizedBy 'startClusterForAz'
afterTests.finalizedBy 'stopAllAfterTests'
compileTestJava.dependsOn ':client:publishToMavenLocal', ':jedis-compatibility:publishToMavenLocal'

tasks.withType(Test) {
    dependsOn ':client:publishToMavenLocal', ':jedis-compatibility:publishToMavenLocal'
    useJUnitPlatform()
    if (!project.gradle.startParameter.taskNames.contains(':integTest:modulesTest')) {
        dependsOn 'beforeTests'
        finalizedBy 'afterTests'
    }

    doFirst {
        systemProperty 'test.server.standalone', standaloneHosts
        systemProperty 'test.server.standalone.tls', standaloneTlsHosts
        systemProperty 'test.server.cluster', clusterHosts
        systemProperty 'test.server.cluster.tls', clusterTlsHosts
        systemProperty 'test.server.azcluster', azClusterHosts
        systemProperty 'test.server.tls', System.getProperty("tls")
    }

    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }

    minHeapSize = "2048m" // Initial heap size. Needed for max size tests.
    maxHeapSize = "2048m" // Maximum heap size. Needed for max size tests.
    
    // Native library path for GLIDE FFI - needed for Jedis compatibility tests
    jvmArgs "-Djava.library.path=${project.rootDir}/../target/release"
    
    // Disable modularity for jedis compatibility tests
    if (name.contains('jedis') || filter.includePatterns.any { it.contains('jedis') }) {
        jvmArgs += "--add-opens=java.base/java.lang=ALL-UNNAMED"
        jvmArgs += "--add-opens=java.base/java.util=ALL-UNNAMED"
    }

    afterTest { desc, result ->
        logger.quiet "${desc.className}.${desc.name}: ${result.resultType} ${(result.getEndTime() - result.getStartTime())/1000}s"
    }

    finalizedBy jacocoTestReport, jacocoTestCoverageVerification
}

// JaCoCo section (code coverage by integration tests)
jacocoTestReport {
    sourceDirectories.setFrom(files("${project.rootDir}/client/src/main/java"))
    classDirectories.setFrom(files("$buildDir/classes"))
    executionData.setFrom(
        files("$buildDir/jacoco/test.exec"),
        files("$buildDir/jacoco/pubsubTest.exec")
    )
}
jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            excludes = [
                'glide.models.protobuf.*',
                'glide.com.google.*', // shadowed protobuf
                'glide.io.netty.*' // shadowed netty
            ]
        }
    }
}
// End of JaCoCo section

test {
    filter {
        excludeTestsMatching 'glide.PubSubTests'
        excludeTestsMatching 'glide.modules.*'
    }
}

tasks.register('pubsubTest', Test) {
    filter {
        includeTestsMatching 'glide.PubSubTests'
    }
    finalizedBy jacocoTestReport, jacocoTestCoverageVerification
}

tasks.register('modulesTest', Test) {
    doFirst {
        clusterHosts = System.getProperty("cluster-endpoints")
    }

    filter {
        includeTestsMatching 'glide.modules.*'
    }
}
