plugins {
    id 'java-library'
    id "com.google.osdetector" version "1.7.3"
}

repositories {
    mavenCentral()
    mavenLocal()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    def classifier 
    if (osdetector.os == 'linux' && osdetector.release.id == 'alpine') {
        classifier = "linux_musl-${osdetector.arch}"
    }
    else {
        classifier = osdetector.classifier
    }
    // Use published GLIDE artifact
    implementation group: 'io.valkey', name: 'valkey-glide', version: project.ext.defaultReleaseVersion, classifier: classifier
    
    // Use published jedis-compatibility artifact
    testImplementation group: 'io.valkey', name: 'valkey-glide-jedis-compatibility', version: project.ext.defaultReleaseVersion, classifier: osdetector.classifier

    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.18.0'
    implementation 'com.google.code.gson:gson:2.13.2'

    // Add commons-pool2 as compile-time dependency for tests
    implementation group: 'org.apache.commons', name: 'commons-pool2', version: '2.12.1'



    // junit
    testImplementation 'org.mockito:mockito-junit-jupiter:3.12.4'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.10.2'

    // semver4j for semantic versioning
    implementation group: 'org.semver4j', name: 'semver4j', version: '5.6.0'

    //lombok
    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'
}

def standaloneHosts = ''
def standaloneTlsHosts = ''
def clusterHosts = ''
def clusterTlsHosts = ''
def azClusterHosts = ''

// Platform-specific Python executable and WSL handling
def isWindows = System.getProperty('os.name').toLowerCase().contains('windows')
def pythonCmd = isWindows ? ['wsl', '--', 'python3'] : ['python3']

// Use integration test IP from environment variable if available, otherwise default to localhost
def clusterHost = System.getenv('VALKEY_INTEG_TEST_IP') ?: '127.0.0.1'

// Get workspace root - use system property or derive from project root
def workspaceRoot = System.getProperty('workspace.root') ?: "${project.rootDir}/../.."

ext {
    extractAddressesFromClusterManagerOutput = { String output ->
        for (def line : output.split("\n")) {
            if (!line.startsWith("CLUSTER_NODES="))
                continue

            return line.split("=")[1]
        }
        return ''
    }
}

tasks.register('stopAllAfterTests') {
    doLast {
        exec {
            workingDir "${project.rootDir}/../utils"
            commandLine *pythonCmd, 'cluster_manager.py', 'stop', '--prefix', 'cluster', '--keep-folder', '--host', clusterHost
            ignoreExitValue true // ignore fail if servers are stopped before
        }
        exec {
            workingDir "${project.rootDir}/../utils"
            commandLine *pythonCmd, 'cluster_manager.py', '--tls', 'stop', '--prefix', 'tls-cluster', '--keep-folder', '--host', clusterHost
            ignoreExitValue true
        }
    }
}

// We need to call for stop before and after the test, but gradle doesn't support executing a task
// twice. So there are two identical tasks with different names.
// We need to call for stop in case if previous test run was interrupted/crashed and didn't stop.
tasks.register('stopAllBeforeTests') {
    doLast {
        exec {
            workingDir "${project.rootDir}/../utils"
            commandLine *pythonCmd, 'cluster_manager.py', 'stop', '--prefix', 'cluster', '--host', clusterHost
            ignoreExitValue true // ignore fail if servers are stopped before
        }
        exec {
            workingDir "${project.rootDir}/../utils"
            commandLine *pythonCmd, 'cluster_manager.py', '--tls', 'stop', '--prefix', 'tls-cluster', '--host', clusterHost
            ignoreExitValue true
        }
    }
}

// delete dirs if stop failed due to https://github.com/valkey-io/valkey-glide/issues/849
tasks.register('clearDirs', Delete) {
    delete "${project.rootDir}/../utils/clusters"
}

tasks.register('startCluster') {
    doLast {
        if (System.getProperty("cluster-endpoints") == null) {
            if (isWindows) {
                println "=== Starting cluster setup directly in WSL ==="
            }
            
            new ByteArrayOutputStream().withStream { os ->
                if (isWindows) {
                    // Use WSL directly without Docker
                    exec {
                        workingDir "${project.rootDir}/../utils"
                        def args = ['wsl', '--', 'python3', 'cluster_manager.py', 'start', '--cluster-mode', '--host', '127.0.0.1']
                        if (System.getProperty("tls") == 'true') args.add(4, '--tls')
                        commandLine args
                        standardOutput = os
                        errorOutput = System.out
                    }
                } else {
                    exec {
                        workingDir "${project.rootDir}/../utils"
                        def args = [*pythonCmd, 'cluster_manager.py', 'start', '--cluster-mode', '--host', clusterHost]
                        if (System.getProperty("tls") == 'true') args.add(2, '--tls')
                        commandLine args
                        standardOutput = os
                        errorOutput = System.out
                    }
                }
                clusterHosts = extractAddressesFromClusterManagerOutput(os.toString())
                println "=== Cluster hosts: ${clusterHosts} ==="
            }
            
            // Check for replica log files after cluster creation
            if (isWindows) {
                println "=== Checking WSL memory usage ==="
                exec {
                    commandLine 'wsl', '--', 'free', '-h'
                    ignoreExitValue true
                }
                
                println "=== Checking for replica log files ==="
                exec {
                    commandLine 'wsl', '--', 'find', '/mnt/c/workspace/utils/clusters', '-name', 'server.log', '-exec', 'ls', '-la', '{}', '\\;'
                    ignoreExitValue true
                }
                
                println "=== Printing contents of all server log files ==="
                exec {
                    commandLine 'wsl', '--', 'find', '/mnt/c/workspace/utils/clusters', '-name', 'server.log', '-exec', 'sh', '-c', 'echo "=== LOG FILE: {} ==="; cat "{}"', '\\;'
                    ignoreExitValue true
                }
                
                println "=== Checking cluster directory structure ==="
                exec {
                    commandLine 'wsl', '--', 'find', '/mnt/c/workspace/utils/clusters', '-type', 'd', '-exec', 'ls', '-la', '{}', '\\;'
                    ignoreExitValue true
                }
                
                println "=== Checking running Valkey processes ==="
                exec {
                    commandLine 'wsl', '--', 'ps', 'aux'
                    ignoreExitValue true
                }
                exec {
                    commandLine 'wsl', '--', 'pgrep', '-f', 'valkey'
                    ignoreExitValue true
                }
            }
        } else {
            clusterHosts = System.getProperty("cluster-endpoints")
        }
    }
}

tasks.register('startClusterForAz') {
    doLast {
        if (System.getProperty("cluster-endpoints") == null) {
            new ByteArrayOutputStream().withStream { os ->
                if (isWindows) {
                    // Use direct WSL for cluster
                    exec {
                        workingDir '../utils'
                        def args = ['wsl', '--', 'python3', 'cluster_manager.py', 'start', '--cluster-mode', '--host', clusterHost]
                        if (System.getProperty("tls") == 'true') args.add(5, '--tls')
                        commandLine args
                        standardOutput = os
                    }
                } else {
                    exec {
                        workingDir "${project.rootDir}/../utils"
                        def args = [*pythonCmd, 'cluster_manager.py', 'start', '--cluster-mode', '--host', clusterHost]
                        if (System.getProperty("tls") == 'true') args.add(2, '--tls')
                        commandLine args
                        standardOutput = os
                    }
                }
                azClusterHosts = extractAddressesFromClusterManagerOutput(os.toString())
            }
        } else {
            azClusterHosts = System.getProperty("cluster-endpoints")
        }
    }
}

tasks.register('beforeTests') {}
tasks.register('afterTests') {}
tasks.register('startStandalone') {
    doLast {
        println "=== DEBUG: startStandalone task starting ==="
        if (isWindows) {
            println "=== Starting standalone setup directly in WSL ==="
            new ByteArrayOutputStream().withStream { os ->
                exec {
                    workingDir '../utils'
                    commandLine 'wsl', '--', 'python3', 'cluster_manager.py', 'start', '--host', '127.0.0.1'
                    standardOutput = os
                    errorOutput = System.err  // Show errors for debugging
                }
                def output = os.toString()
                println "=== Standalone command output: ${output.take(200)}... ==="
                standaloneHosts = extractAddressesFromClusterManagerOutput(output)
                println "=== Standalone hosts: ${standaloneHosts} ==="
            }
        } else {
            println "=== Starting standalone setup with python3 ==="
            new ByteArrayOutputStream().withStream { os ->
                exec {
                    workingDir '../utils'
                    commandLine 'python3', 'cluster_manager.py', 'start', '--host', project.findProperty('host') ?: '127.0.0.1'
                    standardOutput = os
                }
                def output = os.toString()
                println "=== Standalone command output: ${output.take(200)}... ==="
                standaloneHosts = extractAddressesFromClusterManagerOutput(output)
                println "=== Standalone hosts: ${standaloneHosts} ==="
            }
        }
        println "=== DEBUG: startStandalone task completed ==="
    }
}

tasks.register('startStandaloneTls') {
    doLast {
        println "=== DEBUG: startStandaloneTls task starting ==="
        if (isWindows) {
            println "=== Starting standalone TLS setup directly in WSL ==="
            new ByteArrayOutputStream().withStream { os ->
                exec {
                    workingDir '../utils'
                    commandLine 'wsl', '--', 'python3', 'cluster_manager.py', 'start', '--tls', '--host', '127.0.0.1'
                    standardOutput = os
                    errorOutput = System.err  // Show errors for debugging
                }
                def output = os.toString()
                println "=== Standalone TLS command output: ${output.take(200)}... ==="
                standaloneTlsHosts = extractAddressesFromClusterManagerOutput(output)
                println "=== Standalone TLS hosts: ${standaloneTlsHosts} ==="
            }
        } else {
            println "=== Starting standalone TLS setup with python3 ==="
            new ByteArrayOutputStream().withStream { os ->
                exec {
                    workingDir '../utils'
                    commandLine 'python3', 'cluster_manager.py', 'start', '--tls', '--host', project.findProperty('host') ?: '127.0.0.1'
                    standardOutput = os
                }
                def output = os.toString()
                println "=== Standalone TLS command output: ${output.take(200)}... ==="
                standaloneTlsHosts = extractAddressesFromClusterManagerOutput(output)
                println "=== Standalone TLS hosts: ${standaloneTlsHosts} ==="
            }
        }
        println "=== DEBUG: startStandaloneTls task completed ==="
    }
}

tasks.register('startClusterTls') {
    doLast {
        new ByteArrayOutputStream().withStream { os ->
            if (isWindows) {
                // Use direct WSL for TLS cluster
                exec {
                    workingDir '../utils'
                    commandLine 'wsl', '--', 'python3', 'cluster_manager.py', 'start', '--tls', '--cluster-mode', '--host', '127.0.0.1'
                    standardOutput = os
                }
            } else {
                exec {
                    workingDir "${project.rootDir}/../utils"
                    commandLine(*pythonCmd, 'cluster_manager.py', 'start', '--tls', '--cluster-mode')
                    standardOutput = os
                }
            }
            clusterTlsHosts = extractAddressesFromClusterManagerOutput(os.toString())
            
            // Copy certificates from WSL to Windows if needed
            if (isWindows) {
                exec {
                    commandLine 'wsl', '--', 'bash', '-c',
                        'echo "=== WSL tls_crts ===" && ls -la /mnt/d/a/valkey-glide/valkey-glide/utils/tls_crts/ && echo "=== Windows tls_crts ===" && ls -la /mnt/d/a/valkey-glide/valkey-glide/utils/tls_crts/'
                    ignoreExitValue true
                }
                // Ensure certificates have Unix line endings (remove any Windows \r)
                exec {
                    commandLine 'wsl', '--', 'bash', '-c',
                        'if [ -d "${workspaceRoot}/utils/tls_crts" ]; then cd ${workspaceRoot}/utils/tls_crts && for f in *.crt *.key *.pem; do [ -f "$f" ] && sed -i "s/\\r//g" "$f"; done; fi'
                    ignoreExitValue true
                }
            }
        }
    }
}

beforeTests.dependsOn 'stopAllBeforeTests'
stopAllBeforeTests.finalizedBy 'clearDirs'
clearDirs.finalizedBy 'startStandalone'
clearDirs.finalizedBy 'startStandaloneTls'
clearDirs.finalizedBy 'startCluster'
clearDirs.finalizedBy 'startClusterTls'
clearDirs.finalizedBy 'startClusterForAz'
afterTests.finalizedBy 'stopAllAfterTests'
compileTestJava.dependsOn ':client:publishToMavenLocal', ':jedis-compatibility:publishToMavenLocal'

tasks.withType(Test) {
    dependsOn ':client:publishToMavenLocal', ':jedis-compatibility:publishToMavenLocal'
    useJUnitPlatform()
    if (!project.gradle.startParameter.taskNames.contains(':integTest:modulesTest')) {
        dependsOn 'beforeTests'
        finalizedBy 'afterTests'
    }

    doFirst {
        systemProperty 'test.server.standalone', standaloneHosts
        systemProperty 'test.server.standalone.tls', standaloneTlsHosts
        systemProperty 'test.server.cluster', clusterHosts
        systemProperty 'test.server.cluster.tls', clusterTlsHosts
        systemProperty 'test.server.azcluster', azClusterHosts
        systemProperty 'test.server.tls', System.getProperty("tls")
    }

    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }

    minHeapSize = "2048m" // Initial heap size. Needed for max size tests.
    maxHeapSize = "2048m" // Maximum heap size. Needed for max size tests.
    
    // Native library path for GLIDE FFI - needed for Jedis compatibility tests
    jvmArgs "-Djava.library.path=${project.rootDir}/../target/release"
    
    // Disable modularity for jedis compatibility tests
    if (name.contains('jedis') || filter.includePatterns.any { it.contains('jedis') }) {
        jvmArgs += "--add-opens=java.base/java.lang=ALL-UNNAMED"
        jvmArgs += "--add-opens=java.base/java.util=ALL-UNNAMED"
    }

    afterTest { desc, result ->
        logger.quiet "${desc.className}.${desc.name}: ${result.resultType} ${(result.getEndTime() - result.getStartTime())/1000}s"
    }

    finalizedBy jacocoTestReport, jacocoTestCoverageVerification
}

// JaCoCo section (code coverage by integration tests)
jacocoTestReport {
    sourceDirectories.setFrom(files("${project.rootDir}/client/src/main/java"))
    classDirectories.setFrom(files("$buildDir/classes"))
    executionData.setFrom(
        files("$buildDir/jacoco/test.exec"),
        files("$buildDir/jacoco/pubsubTest.exec")
    )
}
jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            excludes = [
                'glide.models.protobuf.*',
                'glide.com.google.*', // shadowed protobuf
                'glide.io.netty.*' // shadowed netty
            ]
        }
    }
}
// End of JaCoCo section

test {
    filter {
        excludeTestsMatching 'glide.PubSubTests'
        excludeTestsMatching 'glide.modules.*'
    }
}

tasks.register('pubsubTest', Test) {
    filter {
        includeTestsMatching 'glide.PubSubTests'
    }
    finalizedBy jacocoTestReport, jacocoTestCoverageVerification
}

tasks.register('modulesTest', Test) {
    doFirst {
        clusterHosts = System.getProperty("cluster-endpoints")
    }

    filter {
        includeTestsMatching 'glide.modules.*'
    }
}
