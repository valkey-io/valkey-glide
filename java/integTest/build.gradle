plugins {
    id 'java-library'
}

repositories {
    mavenCentral()
}

dependencies {
    // client
    implementation project(':client')

    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.13.0'

    // https://github.com/netty/netty/wiki/Native-transports
    // At the moment, Windows is not supported
    implementation group: 'io.netty', name: 'netty-transport-native-epoll', version: '4.1.100.Final', classifier: 'linux-x86_64'
    implementation group: 'io.netty', name: 'netty-transport-native-kqueue', version: '4.1.100.Final', classifier: 'osx-x86_64'
    implementation group: 'io.netty', name: 'netty-transport-native-kqueue', version: '4.1.100.Final', classifier: 'osx-aarch_64'

    // lombok
    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'

    // junit
    testImplementation 'org.mockito:mockito-junit-jupiter:3.12.4'

    // semver4j for semantic versioning
    implementation 'com.vdurmont:semver4j:3.1.0'
}

def standaloneRedisPorts = []
def clusterRedisPorts = []
def redisVersion = ""

ext {
    extractPortsFromClusterManagerOutput = { String output ->
        var res = []
        for (def line : output.split("\n")) {
            if (!line.startsWith("CLUSTER_NODES="))
                continue

            def addresses = line.split("=")[1].split(",")
            for (def address : addresses)
                res << address.split(":")[1]
        }
        return res
    }
    extractRedisVersion = { String output ->
        // Line in format like
        // Redis server v=7.2.3 sha=00000000:0 malloc=jemalloc-5.3.0 bits=64 build=7504b1fedf883f2
        return output.split(" ")[2].split("=")[1]
    }
}

tasks.register('stopAllAfterTests', Exec) {
    workingDir "${project.rootDir}/../utils"
    commandLine 'python3', 'cluster_manager.py', 'stop', '--prefix', 'redis-cluster', '--keep-folder'
}

// We need to call for stop before and after the test, but gradle doesn't support executing a task
// twice. So there are two identical tasks with different names.
// We need to call for stop in case if previous test run was interrupted/crashed and didn't stop.
tasks.register('stopAllBeforeTests', Exec) {
    workingDir "${project.rootDir}/../utils"
    commandLine 'python3', 'cluster_manager.py', 'stop', '--prefix', 'redis-cluster'
    ignoreExitValue true // ignore fail if servers are stopped before
}

// delete dirs if stop failed due to https://github.com/aws/glide-for-redis/issues/849
tasks.register('clearDirs', Delete) {
    delete "${project.rootDir}/../utils/clusters"
}

tasks.register('startCluster') {
    doLast {
        new ByteArrayOutputStream().withStream { os ->
            exec {
                workingDir "${project.rootDir}/../utils"
                commandLine 'python3', 'cluster_manager.py', 'start', '--cluster-mode'
                standardOutput = os
            }
            clusterRedisPorts = extractPortsFromClusterManagerOutput(os.toString())
        }
    }
}

tasks.register('startStandalone') {
    doLast {
        new ByteArrayOutputStream().withStream { os ->
            exec {
                workingDir "${project.rootDir}/../utils"
                commandLine 'python3', 'cluster_manager.py', 'start', '-r', '0'
                standardOutput = os
            }
            standaloneRedisPorts = extractPortsFromClusterManagerOutput(os.toString())
        }
    }
}

tasks.register('getRedisVersion') {
    doLast {
        new ByteArrayOutputStream().withStream { os ->
            exec {
                commandLine 'redis-server', '-v'
                standardOutput = os
            }
            redisVersion = extractRedisVersion(os.toString())
        }
    }
}

test.dependsOn 'stopAllBeforeTests'
test.dependsOn 'getRedisVersion'
stopAllBeforeTests.finalizedBy 'clearDirs'
clearDirs.finalizedBy 'startStandalone'
clearDirs.finalizedBy 'startCluster'
test.finalizedBy 'stopAllAfterTests'
test.dependsOn ':client:buildRustRelease'

tasks.withType(Test) {
    doFirst {
        println "Cluster ports = ${clusterRedisPorts}"
        println "Standalone ports = ${standaloneRedisPorts}"
        println "Redis version = ${redisVersion}"
        systemProperty 'test.redis.standalone.ports', standaloneRedisPorts.join(',')
        systemProperty 'test.redis.cluster.ports', clusterRedisPorts.join(',')
        systemProperty 'test.redis.version', redisVersion
    }

    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }
    jvmArgs "-Djava.library.path=${project.rootDir}/target/release"
    afterTest { desc, result ->
        logger.quiet "${desc.className}.${desc.name}: ${result.resultType} ${(result.getEndTime() - result.getStartTime())/1000}s"
    }
}
