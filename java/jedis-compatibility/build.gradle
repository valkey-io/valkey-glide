plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.freefair.lombok' version '8.6'
    id 'com.google.osdetector' version '1.7.3'
}

repositories {
    mavenCentral()
}

ext {
    // osdetector returns 'aarch_64', but rust triplet has 'aarch64'
    arch = osdetector.arch == 'aarch_64' ? 'aarch64' : osdetector.arch;
}

dependencies {
    implementation project(':client')

    // Transitive dependencies needed by GLIDE client
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.13.0'

    implementation('com.google.protobuf:protobuf-java') {
        version {
            require '[4.29.3,5.0)'
            prefer '4.29.1'
        }
    }

    // Apache Commons Pool 2 for connection pooling - part of public API
    implementation group: 'org.apache.commons', name: 'commons-pool2', version: '2.12.1'

    // Test dependencies
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.10.2'
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '5.2.0'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.2.0'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'

    // SpotBugs annotations for static analysis (compile-only to avoid IDE errors)
    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.8.0'
}

java {
    modularity.inferModulePath = false // Disable module system for compatibility
}

javadoc {
    dependsOn delombok
    source = delombok.outputs
    options.tags = [ "example:a:Example:", "apiNote:a:API Note:" ]
    failOnError = false // Disable javadoc errors for compatibility layer
}

tasks.register('buildRust', Exec) {
    def target
    if (project.hasProperty('target')) {
        target = project.target
    } else {
        if (osdetector.os == 'linux' && osdetector.release.id != 'alpine') {
            target = "${arch}-unknown-linux-gnu.2.17"
        } else if (osdetector.os == 'linux' && osdetector.release.id == 'alpine') {
            target = "${arch}-unknown-linux-musl"
        } else {
            target = ""
        }
    }

    doFirst {
        // Clean all targets to force complete rebuild with new environment
        delete "${projectDir}/target"
        delete "${project.rootDir}/target"
        delete "${project.rootDir}/../target"
        
        // Debug: Print environment variables that will be used
        println "=== JEDIS-COMPATIBILITY BUILD DEBUG ==="
        println "Working directory: ${projectDir}"
        println "GLIDE_NAME environment variable: ${environment.GLIDE_NAME}"
        println "GLIDE_VERSION environment variable: ${environment.GLIDE_VERSION}"
        println "Config file exists: ${file("${projectDir}/.cargo/config.toml").exists()}"
        if (file("${projectDir}/.cargo/config.toml").exists()) {
            println "Config file contents:"
            println file("${projectDir}/.cargo/config.toml").text
        }
        println "========================================="
    }

    print("Building Rust for jedis-compatibility with target: '$target'\n")
    if (target.contains("linux") && !target.contains("musl")) {
        // Remove version suffix for standard Rust target
        def rustTarget = target.replaceAll('\\.2\\.17$', '')
        commandLine 'cargo', 'zigbuild', '--target', "$rustTarget", '--release'
    } else if (target.contains("musl")) {
        commandLine 'cargo', 'zigbuild', '--target', "$target", '--release'
        environment RUSTFLAGS: '-C target-feature=-crt-static'
    } else {
        commandLine 'cargo', 'build', '--release'
    }
    workingDir project.rootDir
    // Set environment variables for the entire build process
    environment GLIDE_NAME: 'GlideJedisAdapter'
    environment GLIDE_VERSION: 'unknown'
}

tasks.register('copyNativeLib', Copy) {
    def target
    def sourceDir
    if (project.hasProperty('target')) {
        target = project.target
        // Remove version suffix for directory path
        def rustTarget = target.replaceAll('\\.2\\.17$', '')
        sourceDir = "${project.rootDir}/target/${rustTarget}/release/"
    } else if (osdetector.os == 'linux' && osdetector.release.id != 'alpine') {
        sourceDir = "${project.rootDir}/target/${arch}-unknown-linux-gnu/release/"
    } else if (osdetector.os == 'linux' && osdetector.release.id == 'alpine') {
        sourceDir = "${project.rootDir}/target/${arch}-unknown-linux-musl/release/"
    } else {
        sourceDir = "${project.rootDir}/target/release/"
    }
    
    from(sourceDir) {
        include "libglide_rs.*"
    }
    
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    into sourceSets.main.output.resourcesDir
}

jar.dependsOn('copyNativeLib')
javadoc.dependsOn('copyNativeLib')
compileTestJava.dependsOn('copyNativeLib')
delombok.dependsOn('compileJava')
copyNativeLib.dependsOn('buildRust')
compileJava.dependsOn(':client:jar')

tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }
    // Native library path for tests
    if (osdetector.os == 'linux' && osdetector.release.id == 'alpine') {
        jvmArgs "-Djava.library.path=${projectDir}/target/${arch}-unknown-linux-musl/release"
    } else {
        jvmArgs "-Djava.library.path=${projectDir}/target/release"
    }
}

jar {
    if (project.hasProperty('target') && project.target.contains("musl")) {
        archiveClassifier = "linux_musl_${arch}"
    } else {
        archiveClassifier = osdetector.classifier
    }
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
    exclude 'glide/models' // exclude protobuf files
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact tasks.named('javadocJar')
            artifact tasks.named('sourcesJar')
            groupId = 'io.valkey'
            artifactId = 'valkey-glide-jedis-compatibility'
            version = System.getenv("GLIDE_RELEASE_VERSION") ?: project.ext.defaultReleaseVersion
            pom {
                name = 'valkey-glide-jedis-compatibility'
                description = 'Jedis compatibility layer for Valkey GLIDE'
                url = 'https://github.com/valkey-io/valkey-glide.git'
                inceptionYear = '2024'
                scm {
                    url = 'https://github.com/valkey-io/valkey-glide'
                    connection = 'scm:git:ssh://git@github.com/valkey-io/valkey-glide.git'
                    developerConnection = 'scm:git:ssh://git@github.com/valkey-io/valkey-glide.git'
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        name = 'Valkey GLIDE Maintainers'
                        url = 'https://github.com/valkey-io/valkey-glide.git'
                    }
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

publishMavenJavaPublicationToMavenLocal.dependsOn jar

tasks.withType(Sign) {
    def releaseVersion = System.getenv("GLIDE_RELEASE_VERSION") ?: project.ext.defaultReleaseVersion;
    def isReleaseVersion = !releaseVersion.endsWith("SNAPSHOT") && releaseVersion != project.ext.defaultReleaseVersion;
    onlyIf("isReleaseVersion is set") { isReleaseVersion }
}

signing {
    sign publishing.publications
}
