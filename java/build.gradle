plugins {
    id 'java'
    id 'java-library'
    id 'io.freefair.lombok' version '6.4.0'
    id 'jacoco'
    id 'com.diffplug.spotless' version '6.19.0'
}

repositories {
    mavenCentral()
}

subprojects {
    repositories {
        mavenCentral()
    }
    // minimal java compatibility level
    plugins.withId('java') {
        sourceCompatibility = targetCompatibility = "11"
    }
    tasks.withType(Test) {
        useJUnitPlatform()

        testLogging {
            exceptionFormat "full"
            events "started", "skipped", "passed", "failed"
            showStandardStreams true
        }
        // TODO: add jacoco with code coverage
        // finalizedBy jacocoTestReport, jacocoTestCoverageVerification
    }
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.9.2'
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

// JaCoCo section (code coverage by unit tests)
jacoco {
    toolVersion = "0.8.9"
}
jacocoTestReport {
    reports {
        xml.configure { enabled false }
        csv.configure { enabled false }
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it)
        }))
    }
}
jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            excludes = [
            ]
            limit {
                counter = 'LINE'
                minimum = 1.0
            }
            limit {
                counter = 'BRANCH'
                minimum = 1.0
            }
        }
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it)
        }))
    }
}
// TODO: add jacoco with code coverage
// check.dependsOn jacocoTestCoverageVerification
// End of JaCoCo section

// Spotless section (code style)
spotless {
    java {
        target fileTree('.') {
            include '**/*.java'
            exclude '**/build/**', '**/build-*/**', '**/protobuf/**'
        }
        importOrder()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
        googleJavaFormat('1.17.0').reflowLongStrings().groupArtifact('com.google.googlejavaformat:google-java-format')
        indentWithTabs(2)
        indentWithSpaces(4)
    }
}
// End of Spotless section
